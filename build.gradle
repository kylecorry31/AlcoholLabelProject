group 'com.emeraldElves.alcohollabelproject'
version '0.1'

apply plugin: 'java'

apply plugin: 'application'
mainClassName = "com.emeraldElves.alcohollabelproject.ui.Main"


sourceCompatibility = 1.8


repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

dependencies {
    compile 'org.apache.commons:commons-lang3:3.1'
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'org.apache.derby:derby:10.11.1.1', 'org.apache.commons:commons-lang3:3.5',  'org.controlsfx:controlsfx:8.40.12'
    compile 'commons-io:commons-io:2.4'
    compile 'com.jfoenix:jfoenix:8.0.2'
    compile 'me.xdrop:fuzzywuzzy:1.1.9'
    compile 'com.opencsv:opencsv:4.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile "org.testfx:openjfx-monocle:8u76-b04" // jdk-9+181 for Java 9
    testCompile "org.testfx:testfx-core:4.0.13-alpha"
    testCompile "org.testfx:testfx-junit:4.0.13-alpha"
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

test {
    outputs.upToDateWhen { false }
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
    from configurations.runtime.collect { zipTree(it) }
}